plugins {
    id 'java'
    id "com.diffplug.spotless" version "6.3.0"
    id 'jacoco'
}

apply plugin : 'eclipse'

dependencies {
    testImplementation project(':jrosmessages')
    testImplementation 'io.github.lambdaprime:id.xfunction:15.0'
    testImplementation('org.junit.jupiter:junit-jupiter-api:5.4.2')
    testImplementation('org.junit.jupiter:junit-jupiter-params:5.4.2')
    testRuntimeOnly('org.junit.jupiter:junit-jupiter-engine:5.4.2')
    testRuntimeOnly('org.junit.platform:junit-platform-commons:1.7.0')
}

sourceCompatibility = '17'
targetCompatibility = '17'

test {
    useJUnitPlatform()
    finalizedBy jacocoTestReport, jacocoTestCoverageVerification
}

jacocoTestReport {
    // generate only HTML report
    reports {
        xml.enabled = false
        csv.enabled = false
        html.enabled = true
    }
    sourceDirectories.from = project(':jrosmessages').sourceSets.main.java.srcDirs
    classDirectories.from = project(':jrosmessages').sourceSets.main.java.classesDirectory
}

jacocoTestCoverageVerification {
    sourceDirectories.from = project(':jrosmessages').sourceSets.main.java.srcDirs
    classDirectories.from = project(':jrosmessages').sourceSets.main.java.classesDirectory
    violationRules {
        rule {
        enabled = true
            element = 'PACKAGE'
            limit {
                counter = 'LINE'
                value = 'COVEREDRATIO'
                minimum = 0.0
            }
        }
    }
}

spotless {
    java {
        importOrder()
        removeUnusedImports()

        // Use Android style which has 4 whitespaces instead 2 by default
        googleJavaFormat().aosp().reflowLongStrings()

        licenseHeader '''/*
 * Copyright $YEAR jrosclient project
 * 
 * Website: https://github.com/lambdaprime/jrosmessages
 * 
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 * 
 *     http://www.apache.org/licenses/LICENSE-2.0
 * 
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
'''
    }
}
// run spotless and format code before the build
testClasses.dependsOn spotlessApply
